topic: ~ExampleDialog()
language: enu

# Defining needed concepts, keywords or sentences corresponding to a concept.
concept:(yes) [yes "all right" sure "why not" ok certainly "very well" yep yea definitely amen]
concept:(no) [no nope "don't want" "no way" never "not at all"]
concept:(how) ["how are you" howdy "what's up" "how do you feel"]
concept:(happy) [ok fine great happy blessed "all right" glad "not bad"]
concept:(sad) [sad miserable bad unhappy tired]
concept:(repeat) [repeat understand]
concept:(thx) ["thank you" thanks gracias "much obliged" merci]
concept:(hello) [hello hi hey "good morning" greetings]
concept:(bye) [goodbye "see you agian" "have a nice day"]
concept:(drink) [coke water coffee tea milktea milk latae amaricano]

u:(~hello) ~hello, Do you want some drinks?
   # u2:(~yes) Good!, What's your name? $playAGame=1
    u1:(~yes) I am going to take your order. What would you like to drink?
        u2:({"I want some" "I want"} _["coke" "water" "juice"]) Good choice, I will remember it and you want some $1 $drink = $1 . What's your name?
            u3:({"My name is" "My name's"} _["Tom" "Jimmy" "Bill"]) Great, I remembered your name is $1 $name = $1 . What's your gender?
                u4:({"I'm" "I am" "a"} _["man" "woman" "male" "female"]) OK thanks, you are $1 $gender = $1 . Finish! I have got your order. $overdi=1
    u1:(~no) Nevermind have a nice day,~bye $no=1
    
#u:(~drink) good choice! $overdi=1
u:("Taking order") The customer is a $gender name $name wants $drink Do you get the order information?
    u1:(~yes) Okay $saything=1
    u1:(~no) The customer is a $gender name $name wants $drink .  Do you get it?
        u2:(~yes) Alright $saything=1 
        u2:(~no) The customer is a $gender name $name wants $drink BYE $saything=1
#u:(My name is _["Tom" "Jimmy" "Bill"]) great, I remembered your name is $1 $name = $1
#u:(I am _["10" "20" "30" "40"] year old) alright, your age is $1 $age     = $1
#u:(I want some _["coke" "water" "juice"]) Good choice, I will remember it and you want some $1 $drink = $1
#u:(I'm _["man" "woman"]) OK thanks, you are $1 $gender = $1
#u:(Information) $name $age $gender $drink




# Start/Wait animation, Goto.
# If User expresses a word or a sentence found in the concept "hello",
# the robot starts Hey_1 animation, answers a random value defined in concept "hello"
# and waits for the end of the animation before going to the label how. Label is expressed this way: %howAreYou
#u:(~hello) ^start(animations/Stand/Gestures/Hey_1) ~hello ^wait(animations/Stand/Gestures/Hey_1) ^gotoReactivate(howAreYou)


# Tag animation.
# If User asks how the robot feels, it starts a random animation being tagged "myself",
# answers and go to the label %howAreYou
u:(~how) ^startTag(myself) I'm fine, thank you. And you, ^gotoReactivate(howAreYou)


# %Label.
# The robot asks how the user feels. 3 kind of answers are expected
# Expressed by concept, user can be happy, sad or ask the robot to repeat the question.
proposal: %howAreYou ^startTag(show) how are you today?
    u1:(~happy) I'm glad you are fine! ^gotoReactivate(favoriteColor)
    u1:(~sad) Oh, I feel sorry for you. ^nextProposal
    u1:(~repeat) I'll repeat for you. ^sameProposal

# Output slot stimulation.
# If User is sad (known from previous question), the robot proposes to play a game.
# If User accepts, it will stimulate the "playAGame" punctual diagram output.
proposal: If you feel like talking about it, don't hesitate. ^nextProposal
proposal: %playAGame Do you want to play a game?
    u1:(~yes) Let's play $playAGame=1
    u1:(~no) Ok, anytime.


# Variables.
# the robot asks User for his prefered color between blue and red.
# this illustrate variables stored in $1 using '_' character.
proposal: %favoriteColor And what is your favorite color between Blue and Red ?
    u1:(violet) Violet is my prefered color too ! ^gotoReactivate(playAGame)
    u1:(_[blue red]) $1 is a very nice color and my prefered is violet. ^gotoReactivate(playAGame)
    u1:(_[green yellow pink orange purple white black]) $1 is a nice color. ^sameProposal


# On any proposal if user asks to repeat, say again the same proposal.
u:(~repeat) I'll repeat for you. ^sameProposal

# Being polite, the robot always answers accordingly to thankfull sentences.
u:(~thx) You're welcome


# Exiting.
# Answering to Goodbye and exiting the dialog box, stimulating the onStopped output slot.
u:(~bye) ~bye $onStopped=1


# Responding to events.
# Here, if the left foot bumper is pressed, the robot starts an animation,
# say a text and waits for the end of the animation.
u:(e:LeftBumperPressed) ^start(animations/Stand/Gestures/YouKnowWhat_1) My left foot bumped into something! ^wait(animations/Stand/Gestures/YouKnowWhat_1)
